#define WORDCALL 1
#define PUSHLIT 2
#define BRANCH 5
#define PRIMITIVE 6

#include primitives.cll

/////////////////////////////////  Main bytecode interpeter //////////////////////////////////////
char dontBreak = 1;
void bytecodeInterp(@int reference)
	int currentFSP = ForthStackPointer;
	dontBreak = char(1);
	while((reference)and(dontBreak))do

		int opType = reference[0];
		if (opType == WORDCALL) then
			@int new_reference = reference[1];
			bytecodeInterp(new_reference[2]); //reference points to the dictionary entry
			reference = reference[2];
		else if(opType == PUSHLIT) then			
			PushLit(reference[1]);
			reference = reference[2];
		else if (opType == BRANCH) then			
			int branchIfTrue = Pop();
			reference = reference[1] if branchIfTrue else reference[2];
		else if (opType == PRIMITIVE) then
			executePrimitive(reference[1]);
			 reference = reference[2];
		else
			printf("ERROR: unrecognised bytecode: "); print_i(opType);
			dontBreak = char(0);
		endif;
		endif;
		endif;
		endif;
	loop;	
	if (not(dontBreak)) then ForthStackPointer = currentFSP; endif; //if there is an error then roll back the FSP.
end;

/////////////////////////////// Stack tools for bytecode interpeter /////////////////////////////////
#define FORTHSTACKSIZE 65536
@int FORTHSTACKSIZE ForthStack;
int ForthStackPointer = 0;

void PushLit(int number)
	ForthStack[ForthStackPointer] = number;
	ForthStackPointer = ForthStackPointer + 1;
	if (ForthStackPointer>FORTHSTACKSIZE) then
		printf("ERROR: Forth Stack Overflow.\n");
		dontBreak = char(0); //breaks to the REPL 
	endif;
end;


void executePrimitive(int primitive_opcode)
	//picks primitive to run and runs it
	//binary search over opcodes
	if (primitive_opcode<32) then 
		if (primitive_opcode<16) then
			if (primitive_opcode<8) then
				if (primitive_opcode<4) then
					if (primitive_opcode<2) then
						if (primitive_opcode<1) then // == 00:  @
							Fetch();
						else 						 // == 01: !
							Store();
						endif;
					else
						if (primitive_opcode<3) then // == 02: .
							printTOS();
						else 						 // == 03: DUP
							Dup();
						endif;
					endif;
				else
					if (primitive_opcode<6) then
						if (primitive_opcode<5) then // == 04: SWAP
							Swap();
						else 						 // == 05: ROT
							Rot();
						endif;
					else
						if (primitive_opcode<7) then // == 06: OVER
							Over();
						else 						 // == 07: DROP
							Drop();
						endif;
					endif;
				endif;
			else
				if (primitive_opcode<12) then
					if (primitive_opcode<10) then
						if (primitive_opcode<9) then // == 08: +
							ADD();
						else						 // == 09: -
							SUB();
						endif;
					else
						if (primitive_opcode<11) then // == 0A: *
							MUL();
						else						  // == 0B: /
							DIV();
						endif;
					endif;
				else
					if (primitive_opcode<14) then
						if (primitive_opcode<13) then // == 0C: %
							MOD();
						else 						  // == 0D: &
							AND();
						endif;
					else
						if (primitive_opcode<15) then // == 0E: |
							OR();
						else 						  // == 0F: ^
							XOR();
						endif;
					endif;
				endif;
			endif;
		else
			if (primitive_opcode<24) then
				if (primitive_opcode<20) then
					if (primitive_opcode<18) then
						if (primitive_opcode<17) then // == 10: ~
							NOT();
						else 						 // == 11: ~&
							NAND();
						endif;
					else
						if (primitive_opcode<19) then // == 12: ~|
							NOR();
						else 						  // == 13: ~^
							XNOR();
						endif;
					endif;
				else
					if (primitive_opcode<22) then
						if (primitive_opcode<21) then // == 14: <<
							SHL();
						else 						  // == 15: >>
							SHR();
						endif;
					else
						if (primitive_opcode<23) then // == 16: <
							less();
						else 						  // == 17: >
							greater();
						endif;
					endif;
				endif;
			else
				if (primitive_opcode<28) then
					if (primitive_opcode<26) then
						if (primitive_opcode<25) then // == 18: ==
							equal();
						else						  // == 19: VARIABLE
							createVariable();
						endif;
					else
						if (primitive_opcode<27) then // == 1A: CONST
							createConstant();
						else						  // == 1B: TABLE
							createTable();
						endif;
					endif;
				else
					if (primitive_opcode<30) then
						if (primitive_opcode<29) then // == 1C: READ
							primRead();
						else 						  // == 1D: EXEC
							primExecute();
						endif;
					else
						if (primitive_opcode<31) then // == 1E: INDICT
							primIsInDict();
						else 						  // == 1F: COMPILE
							primCompile();
						endif;
					endif;
				endif;
			endif;
		endif;
	else
		if (primitive_opcode<40) then
			if (primitive_opcode<36) then
				if (primitive_opcode==32) then 			  // == 20: ISINT
					primIsInt();
				else if (primitive_opcode == 33) then		  // == 21: CONVERTINT
					primConvertInt();
				else if (primitive_opcode == 34) then	  // == 22: .BUF
					printBUF();
				else if (primitive_opcode == 35) then	  // == 23: .c
					printChar();
				endif; endif; endif; endif;
			else
				if (primitive_opcode == 36) then	  	   // == 24: PASS
					primPass();

				else if (primitive_opcode == 37) then 	   // == 25: .s
					printStack(); 						  
				else if (primitive_opcode == 38) then 	   // == 26: .dict
					printDict();
				else									   // == 27: HALT
					halt();
				endif;
				endif;endif;
			endif;	
		else if (primitive_opcode == 40) then 			   // == 28: DROPALL
				dropAll();
			else if (primitive_opcode == 41) then 		   // == 29: //
				comment();
			else if (primitive_opcode == 42) then 		   // == 2A: READC
				readC();
			else if (primitive_opcode == 43) then		   // == 2B: ALLOC
				primAlloc();
			else if (primitive_opcode == 44) then 		   // == 2C: DALLOC
				primDalloc();
			else if (primitive_opcode == 45) then 		   // == 2D: SETDICT
				setDict();
			else if (primitive_opcode == 46) then 		   // == 2E: BOOTSTRP
				bootstrap();
			else if (primitive_opcode == 47) then 		   // == 2F: @DISK
				READDISK();
			else if (primitive_opcode == 48) then		   // == 30: !DISK
				WRITEDISK();
			else
				printf("ERROR: unrecognised opcode ");print_h(primitive_opcode);dontBreak = char(0);
			endif; endif; endif; endif; endif; endif; endif; endif; endif;
		endif;
	endif;
end;