//////////////////////// INNER INTERPRETER TECHNICAL PRIMITIVES ///////////////

//primitives not designed to be used by the user, but used to build the outer interpreter
void primRead() // ( -- ) [ reads a word into the buffer ]
	if ((EOL)and(READ_IN_MODE == KEYBOARD)) then printf("\nOK\n"); printf("\n>>"); EOL = 0; endif;
	getWordName(); //puts the word name into the global buffer
end;

void primIsInDict() // ( -- a ) [ tests word in buffer against the dictionary. a = 0 if not found ] 
	//primitive version of is in dict
	Push(isInDict(WORD_BUF,ForthDictHead));
end;

void primExecute() // ( ref -- ) [ executes bytecode at the address passed (needs to be in dictionary entry format)]
	@int ref = Pop();
	bytecodeInterp(ref[2]);
	dontBreak = 1; //if a subroutine causes a break then the break doesn't unwravel past the outer interpreter
end;

void primCompile() // ( -- ) [ starts the compiler ]
	compileWord();
end;

void primIsInt() // ( -- bool ) [ tests if the string in the word buffer an integer, if so then returns 1 else 0]
	Push(isInt()); //if hasnt returned then is an int
end;

void primConvertInt() // ( -- val ) [ converts the word buffer to an integer ]
	//converts WORD_BUF to an int and pushes it	
	Push(convertInt());
end;

//////// Variables and constant auxiliaries (technical primitives) //////////
void createConstant() // ( val -- ) [ reads a word from the input, creates a constant with that word as a name which pushes the val ]
	@char 9 name; //9 to pad a zero on the end
	getWordName();   //produces an acceptable name
	copyName(name);
	int n = Pop(); 					 //n on stack
	addToDict(name,newByteCodeNode(PUSHLIT,n,0)); //creates a word
end;

void createVariable() // ( -- ) [ reads a word from the input, creates a variable with that word as a name which pushes the address of the variable ]
	@char 9 name; //9 to pad a zero on the end
	getWordName();   //produces an acceptable name
	copyName(name);
	@int address = malloc(1); 					 //allocates memory
	addToDict(name,newByteCodeNode(PUSHLIT,address,0)); //creates a word
end;

void createTable() // ( size -- ) [ reads a word from the input, creates a variable with that word as a name which pushes the address of a table of size size ]
	@char 9 name; //9 to pad a zero on the end
	getWordName();   //produces an acceptable name
	copyName(name);
	@int address = malloc(Pop()); 					 //n on stack
	addToDict(name,newByteCodeNode(PUSHLIT,address,0)); //creates a word
end;

void dropAll() // ( whole stack -- ) [ deletes everything on the stack ]
	ForthStackPointer = 0;
end;
void comment()
	scrollLine();
end;

void setDict() // ( ptr -- ) [ sets the global head of dict pointer to ptr ]
	ForthDictHead = Pop();
end;

void getDict() // ( -- ) [ returns the global head of dict pointer ]
	Push(ForthDictHead);
end;