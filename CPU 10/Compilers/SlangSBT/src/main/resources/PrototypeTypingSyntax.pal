ElementaryTokens:
	" " | "\t" | "\n" | "\r" | "(" | ")" | "-"| "~"| "+"| "-"| "*"| "<"| "=" | "&" | "|" | ">" | "?" | ":" | ";" | "," | "!"
Ignore:
	" " | "\t" | "\n" | "\r"
Terminals:
	lower -> "a"|"b"|"c"|"d"|"e"|"f"|"g"|"h"|"i"|"j"|"k"|"l"|"m"|"n"|"o"|"p"|"q"|"r"|"s"|"t"|"u"|"v"|"w"|"x"|"y"|"z"
	upper -> "A"|"B"|"C"|"D"|"E"|"F"|"G"|"H"|"I"|"J"|"K"|"L"|"M"|"N"|"O"|"P"|"Q"|"R"|"S"|"T"|"U"|"V"|"W"|"X"|"Y"|"Z"
	letter -> upper|lower
	digit -> "0"|"1"|"2"|"3"|"4"|"5"|"6"|"7"|"8"|"9"
	string_symbol -> "_"
	id -> (letter|string_symbol).(letter|digit|string_symbol)*
	num -> digit.digit*
	what -> "?"

Grammar:

    -- Rules which define addition of definable types
	PROGRAM -> expr

	arrow -> "-" ">"
	assign -> ":" "="
	and ->  "&" "&"
	or -> "|" "|"
	unit -> "(" ")"

	uop -> "-" | "~" | "ref" | "!" | "snd" | "fst" | "inl" t_expr | "inr" t_expr
	mulop ->  "*" | and | or | "=" | "<"
	addop -> "+" | "-"
	assop -> assign

	expr -> expr addop term | term | lambda | let | assignment | while_loop | seq | case_expr | tDefinition | pMatch -- changed seq rule too

	term -> term mulop factor | factor

	factor -> ternary_expr | application

	application ->  application expr | value



	seq -> expr | expr ";" seq
	ternary_expr -> "if" expr "then" expr "else" expr "end"
	while_loop -> "while" expr "do" expr "end"
	lambda -> "fun" "(" id opt_t_expr ")" arrow expr "end"
	let -> "let" let_defs "in" expr "end"


    let_defs -> let_defs ";" let_def | let_def
    let_def -> let_val | let_fun

	let_fun -> "fun" id "(" id opt_t_expr ")" opt_t_expr "=" expr
	let_val -> "val" id opt_t_expr "=" expr

	assignment -> value assop expr
	case_expr -> "case" expr "of" "inl" "(" id opt_t_expr ")" arrow expr "|" "inr" "(" id opt_t_expr ")" arrow expr "end"



	const -> num | unit | "true" | "false" | what
	value -> const | id | "(" expr ")" | "(" expr "," expr ")" | uop value -- no need to define constructor syntax, since application(id, value) does it for us
	                                                                        -- consider constructors as fn args -> type

	opt_t_expr -> ":" t_expr | ""


tParam -> "'" id
tDefinition -> "datatype" tBody ";" expr
tBody -> tParamPrefix id "=" tConstruction tCase tSuffix
tConstruction -> id constrSuffix
constrSuffix -> "" | "of" t_expr

tCase -> "" | "|" tConstruction tCase
tSuffix -> "" | "and" tBody

tParamPrefix -> "" | tParam | "(" tParamList ")"
tParamList -> tParam | tParamList "," tParam


	t_expr -> t_sum arrow t_expr | t_sum
	t_sum -> t_sum "+" t_term | t_term
	t_term -> t_term "*" t_apply | t_apply
	t_apply -> t_fact t_parametrised | t_fact
	t_parametrised -> t_fact t_parametrised | id
	t_fact -> "bool" | "int" | "unit" | "(" t_expr ")" | t_expr "ref" | id | tParam



matchWith -> "match" expr "with" pMatch
pValue -> id | num | unit | "true" | "false"
pTerm -> pValue | id "(" pTermList ")"
pTermList -> pTerm | pTermList "," pTerm

pMatch -> pTerm arrow expr pSuffix
pSuffix -> "" | "|" pMatch
