ElementaryTokens:
	" " | "\t" | "\n" | "\r" | "(" | ")" | "-"| "~"| "+"| "-"| "*"| "<"| "=" | "&" | "|" | ">" | "?" | ":" | ";" | "," | "!"
Ignore:
	" " | "\t" | "\n" | "\r"
Terminals:
	lower -> "a"|"b"|"c"|"d"|"e"|"f"|"g"|"h"|"i"|"j"|"k"|"l"|"m"|"n"|"o"|"p"|"q"|"r"|"s"|"t"|"u"|"v"|"w"|"x"|"y"|"z"
	upper -> "A"|"B"|"C"|"D"|"E"|"F"|"G"|"H"|"I"|"J"|"K"|"L"|"M"|"N"|"O"|"P"|"Q"|"R"|"S"|"T"|"U"|"V"|"W"|"X"|"Y"|"Z"
	letter -> upper|lower
	digit -> "0"|"1"|"2"|"3"|"4"|"5"|"6"|"7"|"8"|"9"
	string_symbol -> "_"
	id -> (letter|string_symbol).(letter|digit|string_symbol)*
	num -> digit.digit*
	what -> "?"

Grammar:
	PROGRAM -> expr

	arrow -> "-" ">"
	assign -> ":" "="
	and ->  "&" "&"
	or -> "|" "|"
	unit -> "(" ")"

	uop -> "-" | "~" | "ref" | "!" | "snd" | "fst" | "inl" t_expr | "inr" t_expr
	mulop ->  "*" | and | or | "=" | "<"
	addop -> "+" | "-"
	assop -> assign

	t_expr -> t_sum arrow t_expr | t_sum
	t_sum -> t_sum "+" t_term | t_term
	t_term -> t_term "*" t_fact | t_fact
	t_fact -> "bool" | "int" | "unit" | "(" t_expr ")" | t_expr "ref"

	expr -> sum | lambda | let | assignment | while_loop | "begin" seq "end" | case_expr

    sum -> sum addop term | term

	term -> term mulop factor | factor

	factor -> ternary_expr | composition

	composition ->  composition expr | value



	seq -> expr | expr ";" seq
	ternary_expr -> "if" expr "then" expr "else" expr "end"
	while_loop -> "while" expr "do" expr "end"
	lambda -> "fun" id opt_t_expr arrow expr
	let -> "let" let_defs "in" expr


    let_defs -> let_defs ";" let_def | let_def
    let_def -> let_val | let_fun

	let_fun -> "fun" id opt_t_expr arg_list "=" expr
	let_val -> "val" id opt_t_expr "=" expr

	arg_list -> id opt_t_expr | id opt_t_expr arg_list

	assignment -> value assop expr
	case_expr -> "case" expr "of" "inl" "(" id opt_t_expr ")" arrow expr "|" "inr" "(" id opt_t_expr ")" arrow expr "end"



	const -> num | unit | "true" | "false" | what
	value -> const | id | "(" expr ")" | "(" expr "," expr ")" | uop value

	opt_t_expr -> ":" t_expr | ""